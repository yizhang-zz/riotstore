#!/usr/bin/env gnuplot

set term pdf enhanced monochrome size 2,1.6 fsize 7
set style data histogram
set style histogram rowstacked title offset 0,-1
set boxwidth 0.9
set style fill pattern border
set lmargin 7
set rmargin 1
set bmargin 1.5

set key off
set noxtics 
set output 'time.pdf'
set yrange [0:*]
set ylabel "I/O + CPU = total time (10^2s)" offset 1,0
set label "LAB" at 0,3 rotate left
set label "B" at 1,5.5 rotate left
set label "DAF" at 2,1.8 rotate left
plot \
newhistogram "row/block" lt 1,\
'row-major/data.txt' u ($5+$6)/100 fs pattern 2, '' u ($7-$5-$6)/100 fs pattern 4,\
newhistogram "bit-reversal/block" lt 1,\
'bit-reversal/data.txt' u ($5+$6)/100 notitle fs pattern 2, '' u ($7-$5-$6)/100 notitle fs pattern 4

set output 'nleaves.pdf'
set yrange [0:15]
unset label
set ylabel "# pages allocated (10^5)" offset 1,0
set label "LAB" at 0,4.5 rotate left
set label "B" at 1,13.5 rotate left
set label "DAF" at 2,4.5 rotate left
plot \
newhistogram "row/block" lt 1,\
'row-major/data.txt' u ($2)/1e5 fs pattern 1, \
newhistogram "bit-reversal/block" lt 1,\
'bit-reversal/data.txt' u ($2)/1e5 fs pattern 1

